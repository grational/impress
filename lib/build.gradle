/*
 * This build file was auto generated by 'gigawatt'
 * @author gsus
 * @date 17-04-2025 07:32
 */

// [plugins] this block must be the first {{{
plugins {
	// support for groovy
	id 'groovy'
	// support for the api keyword in the dependencies section
	id 'java-library'
	// required by jitpack
	id 'maven-publish'
	// awesome git tag / gradle version integration
	id 'pl.allegro.tech.build.axion-release' version '1.21.1'
	// docker-compose support
	id 'com.avast.gradle.docker-compose' version '0.17.21'
}
// }}}

// [flavor] compatibility matrix {{{
project.ext {
	// update to the latest stable Java version whenever possible
	toolchainVersion = 25
	// To switch flavors, change these values:
	jvmVersion       = 8
	groovy           = [ major: 3, minor: 0, patch: 25 ]
	groovyGroup      = groovy.major >= 4 ? 'apache' : 'codehaus'
	groovyVersion    = groovy.values().join('.')
	flavor           = "java-${jvmVersion}-groovy-${groovy.major}"
	spockVersion     = "2.4-groovy-${groovy.major}.${groovy.minor}"
}
// }}}

project.with { // {{{
	group = 'it.grational'
	version = scmVersion.version
	description = 'Impress - A Groovy library to store your objects into any storage, with an implementation for AWS DynamoDB'
} // }}}

java { // {{{
	toolchain {
		languageVersion = JavaLanguageVersion.of(project.toolchainVersion)
	}
	targetCompatibility = JavaVersion.toVersion(project.jvmVersion)
} // }}}

// [dependencies] {{{
repositories {
	mavenCentral()
	maven { url = "https://jitpack.io" }
}

dependencies {
	api (
		"org.${groovyGroup}.groovy:groovy:${groovyVersion}",
		"software.amazon.awssdk:dynamodb:2.41.25"
	)

	testImplementation "org.spockframework:spock-core:$spockVersion"

	// Gradle 9 no longer pulls JUnit Platform launcher implicitly
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}
// }}}

// test configuration {{{
tasks.withType(Test) {
	useJUnitPlatform()
	
	// Smart parallelism: Unit tests can run in parallel, 
	// but we cap it to avoid memory pressure or DB contention.
	maxParallelForks = Math.max (
		1,
		Math.min(4, (int)(Runtime.runtime.availableProcessors() / 2))
	)
	
	if (project.toolchainVersion >= 9) {
		jvmArgs('--add-opens=java.base/java.util=ALL-UNNAMED')
	}
}

// Ensure clean builds
jar.dependsOn 'check'
check.dependsOn 'clean'

// Specialized test tasks
task uniTest(type: Test) {
	description = 'Runs unit tests.'
	exclude '**/*ISpec*', '**/*FSpec*'
}

task intTest(type: Test) {
	description = 'Runs integration tests.'
	exclude '**/*USpec*', '**/*FSpec*'
}

task funTest(type: Test) {
	description = 'Runs functional tests.'
	exclude '**/*USpec*', '**/*ISpec*'
}

// Hide custom tasks from default 'test' run if needed
gradle.startParameter.excludedTaskNames += ["uniTest", "intTest", "funTest"]
// }}}

// [publishing] JitPack & Maven Central layout {{{
publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.github.grational'
			artifactId = 'impress'
			version = scmVersion.version
			from components.java
		}
	}
}
// }}}

// [infrastructure] docker-compose for DynamoDB {{{
tasks.composeUp {
	onlyIf { containersNeeded() }
}

test.dependsOn tasks.composeUp

dockerCompose {
	useComposeFiles = ['docker-compose.yml']
	waitForTcpPorts = true
	useDockerComposeV2 = false
	// clean the environment after tests to avoid leftovers
	stopContainers = true
	removeContainers = true
	removeVolumes = true
}

boolean containersNeeded() {
	def cli = gradle.startParameter.taskRequests
	
	boolean composeSpec = cli.any {
		it.args.any { arg ->
			arg =~ /CSpec/
		}
	}
	
	boolean allSpecs = cli.any {
		it.args.size() == 1 && it.args[0] == 'test'
	}

	return composeSpec || allSpecs
}
// }}}

// vim: ft=groovy:fdm=marker
